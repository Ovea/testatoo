= Testatoo documentation
v2.0, 2014
:toc:
:doctype: book
:icons: font
:homepage: https://github.com/Ovea/testatoo
:desc: This is the documentation of Testatoo
:testaoo-version: 2.0.b1

:source-highlighter: highlightjs
:imagesdir: images

image:logo.png[float="left"]

= Introduction

Testatoo is a web user interface testing tool. It's the result of numerous real-world observations of developers in the trenches in the area of GUI testing.
Working for many years to promote the TDD approaches, we often faced difficulties in their implementation for the graphical layer of applications.

The "test FIRST" principle excludes all scenario recorder based approaches that only allow you to write a posteriori tests.
Our experience has taught us that this path is a dead end (but we reserve this for another discussion...).

Another problem is UI tests are brittle and costly! We do think that this is due to the lack of abstraction in existing UI testing tools.

Testatoo provides on one hand an abstraction of the UI business domain through an expressive API and on the other hand a way to express this domain via a DSL (a button semantically stays a buttons whatever the technology).
With Testatoo you can therefore write tests with a seldom achieved level of expressiveness and make these tests INDEPENDENT of the underlying technology.

Testatoo can therefore transform the test in real assets, present throughout the life of the application and always in tune with the latest version of the application.

= Technologies

Testatoo is built on top of http://code.google.com/p/selenium/[WebDriver], it can work with any browser supported by https://code.google.com/p/selenium/wiki/FrequentlyAskedQuestions[WebDriver] (IE, Google-chrome, Firefox, ...).
If Testatoo provides, like http://www.gebish.org/testing[geb], an extra layer of convenience and productivity. It's always possible to "fallback" to the WebDriver level to do something directly should you need to.
But Testatoo adds a powerful DSL while maintaining the usage of Groovy and keeps the advantage of a strong typed language.

Example of a test syntax :

[source, groovy]
-------------------------------------------------------------------------------
    assertThat textField has label('myLabel')
    assertThat textField has placeholder('My placeholder')
    assertThat textField is empty
-------------------------------------------------------------------------------

= Why should I use Testatoo

If you don't use Testatoo for your UI tests it's like you "Knocking On Heaven's Door".

= Installation & Usage (I want to ride my bicycle)

One snipet of code is worth a thousand words
So go ......

Get Testatoo and dependencies libraries via Mavenâ€¦

[source, xml]
-------------------------------------------------------------------------------
<dependency>
    <groupId>org.testatoo</groupId>
    <artifactId>testatoo-core</artifactId>
    <version>{testaoo-version}</version>
    <scope>test</scope>
</dependency>
<dependency>
    <groupId>junit</groupId>
    <artifactId>junit</artifactId>
    <version>4.11</version>
    <scope>test</scope>
</dependency>
<dependency>
    <groupId>org.seleniumhq.selenium</groupId>
    <artifactId>selenium-java</artifactId>
    <version>2.39.0</version>
    <scope>test</scope>
</dependency>
-------------------------------------------------------------------------------

Testatoo work with existing popular unit test libraries like JUnit.
In our first test (BaseGoogleTest) we test a simple search on Google.

The test looks like :

[source, groovy]
-------------------------------------------------------------------------------
package starter

import org.junit.AfterClass
import org.junit.BeforeClass
import org.junit.Test
import org.junit.runner.RunWith
import org.junit.runners.JUnit4
import org.openqa.selenium.firefox.FirefoxDriver
import org.testatoo.core.Testatoo
import org.testatoo.core.component.Button
import org.testatoo.core.component.input.TextField
import org.testatoo.core.component.list.ListView
import org.testatoo.core.evaluator.webdriver.WebDriverEvaluator

import static org.testatoo.core.Testatoo.*
import static org.testatoo.core.input.Mouse.*
import static org.testatoo.core.property.Properties.*
import static org.testatoo.core.state.States.*

@RunWith(JUnit4)
class BaseGoogleTest {

    @BeforeClass
    public static void setup() {
        Testatoo.evaluator =  new WebDriverEvaluator(new FirefoxDriver())
        open 'http://www.google.com'
    }
    @AfterClass public static void tearDown() { evaluator.close() }

    @Test
    public void simple_test() {
        TextField searchField = $('#gbqfq') as TextField
        Button searchButton = $('#gbqfb') as Button
        ListView resultList = $('#rso') as ListView

        assertThat resultList is missing
        assertThat searchField is visible

        on searchField enter 'Testatoo'
        clickOn searchButton

        waitUntil {resultList.is visible}

        assertThat resultList.items[0] has value.containing('Testatoo')
    }

}
-------------------------------------------------------------------------------

If you run the test it should process a simple search on google engine.
Some interesting element can be show in this test :

Testatoo work with Strongly typed components:: Testatoo manipulate typed components like TextField, Button, ListView.
                                         The power of Testatoo is in one hand to provide a rich list of components and on the other one the possibility
                                         to easily create and compose your own components set.

Testatoo use deferred evaluation:: As you can see we instantiate a ListView event if the component is not available in the page.
                                   You can test if the component is missing

Testatoo DSL is very clean and functional:: Testatoo is based on a clear and powerful syntax.
Testatoo code never refer to a technical layer:: Under no circumstances in this test the underline DOM technology transpire in our code.

[NOTE]
===============================
As a rule when you create a test with Testatoo we recommend to add some default import to facilitate the automatic completion on your favorite IDE(A).

[source, groovy]
-------------------------------------------------------------------------------

import static org.testatoo.core.Testatoo.*
import static org.testatoo.core.input.Mouse.*
import static org.testatoo.core.property.Properties.*
import static org.testatoo.core.state.States.*

-------------------------------------------------------------------------------
===============================

= Back to the future

Well well well (My Michelle), as explained Testatoo is library to write functional test. Functional test mean SPECIFICATION.

As a SPECIFIER I want to express my test (intention) BEFORE to code my UI. So if the intention is to have
a page with two radio buttons to select the gender (male or female). The test can be expressed like this :

-------------------------------------------------------------------------------
assert that the maleRadio is unchecked
and it has label "Male"

assert that the femaleRadio is unchecked
and it has label "Female"

check the maleRadio
assert that the maleRadio is checked
assert that the femaleRadio is unchecked

check the femaleRadio
assert that the femaleRadio checked
assert that the maleRadio is unchecked
-------------------------------------------------------------------------------

The Testatoo syntax to express this specification is :

[source, groovy]
-------------------------------------------------------------------------------
assertThat {
    maleRadio.is(unchecked) and maleRadio.has label('Male')
    femaleRadio.is(unchecked) and femaleRadio.has label('Female')
}

check maleRadio
assertThat maleRadio is checked
assertThat femaleRadio is unchecked

check femaleRadio
assertThat femaleRadio is checked
assertThat maleRadio is unchecked
-------------------------------------------------------------------------------

Pretty close isn't it ;)

Testatoo provides a DSL to write tests in a natural language easy to manipulate for a specifications point of view.
As this is a high level language, it allows tests to be written before UI coding.

YES, for the first time with Testatoo you can write UI functional test FIRST (yeah, it's pretty cool)









