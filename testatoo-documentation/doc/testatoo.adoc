= Testatoo documentation
v2.0, 2014
:toc:
:doctype: book
:icons: font
:homepage: https://github.com/Ovea/testatoo
:desc: This is the documentation of Testatoo
:testaoo-version: 2.0.b1

:source-highlighter: highlightjs
:imagesdir: images

image:logo.png[float="left"]

== Introduction

Testatoo is a web user interface testing tool. It's the result of numerous real-world observations of developers in the trenches in the area of GUI testing.
Working for many years to promote the TDD approaches, we often faced difficulties in their implementation for the graphical layer of applications.

The "test FIRST" principle excludes all scenario recorder based approaches that only allow you to write a posteriori tests.
Our experience has taught us that this path is a dead end (but we reserve this for another discussion...).

Another problem is UI tests are brittle and costly! We do think that this is due to the lack of abstraction in existing UI testing tools.

Testatoo provides, on one hand, an abstraction of the UI business domain through an expressive API and, on the other hand, a way to express this domain via a DSL (a button semantically stays a button, whatever the technology).
With Testatoo, you can therefore write tests with a seldom achieved level of expressiveness and make those tests INDEPENDENT of the underlying technology.

Testatoo can therefore transform the tests in real assets, present throughout the life of the application and always in tune with the latest version of the application.

== Technologies

Testatoo is built on top of http://code.google.com/p/selenium/[WebDriver], it can work with any browser supported by https://code.google.com/p/selenium/wiki/FrequentlyAskedQuestions[WebDriver] (IE, Google-chrome, Firefox, ...).
Even though Testatoo provides, like http://www.gebish.org/testing[geb], an extra layer of convenience and productivity, it's always possible to "fallback" to the WebDriver level to do something directly, should you need to.
But Testatoo adds a powerful DSL, while maintaining the usage of Groovy and keeps the advantages of a strongly typed language.

Example of a test syntax :

[source, groovy]
-------------------------------------------------------------------------------
    expect textField has label('myLabel')
    expect textField has placeholder('My placeholder')
    expect textField is empty
-------------------------------------------------------------------------------

== Why should I use Testatoo?

If you don't use Testatoo for your UI tests it's like you "Knocking On Heaven's Door".

== Installation & Usage (I want to ride my bicycle!)

One snippet of code is worth a thousand words.
So let's go...

Get Testatoo and dependencies libraries via Maven...

[source, xml]
-------------------------------------------------------------------------------
<dependency>
    <groupId>org.testatoo</groupId>
    <artifactId>testatoo-core</artifactId>
    <version>{testaoo-version}</version>
    <scope>test</scope>
</dependency>
<dependency>
    <groupId>junit</groupId>
    <artifactId>junit</artifactId>
    <version>4.11</version>
    <scope>test</scope>
</dependency>
<dependency>
    <groupId>org.seleniumhq.selenium</groupId>
    <artifactId>selenium-java</artifactId>
    <version>2.39.0</version>
    <scope>test</scope>
</dependency>
-------------------------------------------------------------------------------

Testatoo works with existing popular unit test libraries, like JUnit.
In our first test (BaseGoogleTest), we test a simple search on Google.

The test looks like this :

[source, groovy]
-------------------------------------------------------------------------------
package starter

import org.junit.AfterClass
import org.junit.BeforeClass
import org.junit.Test
import org.junit.runner.RunWith
import org.junit.runners.JUnit4
import org.openqa.selenium.firefox.FirefoxDriver
import org.testatoo.core.Testatoo
import org.testatoo.core.component.Button
import org.testatoo.core.component.input.TextField
import org.testatoo.core.component.list.ListView
import org.testatoo.core.evaluator.webdriver.WebDriverEvaluator

import static org.testatoo.core.Testatoo.*
import static org.testatoo.core.input.Mouse.*
import static org.testatoo.core.property.Properties.*
import static org.testatoo.core.state.States.*

@RunWith(JUnit4)
class BaseGoogleTest {

    @BeforeClass
    public static void setup() {
        Testatoo.evaluator =  new WebDriverEvaluator(new FirefoxDriver())
        open 'http://www.google.com'
    }
    @AfterClass public static void tearDown() { evaluator.close() }

    @Test
    public void simple_test() {
        TextField searchField = $('#gbqfq') as TextField
        Button searchButton = $('#gbqfb') as Button
        ListView resultList = $('#rso') as ListView

        expect resultList is missing
        expect searchField is visible

        on searchField enter 'Testatoo'
        clickOn searchButton

        waitUntil {resultList.is visible}

        expect resultList.items[0] has value.containing('Testatoo')
    }

}
-------------------------------------------------------------------------------

If you run the test, it should process a simple search on google engine.
Some interesting elements can be shown in this test :

Testatoo works with Strongly typed components:: Testatoo manipulates typed components like TextField, Button, ListView.
                                         The power of Testatoo is in one hand to provide a rich list of components, and on the other one the possibility
                                         to easily create and compose your own components set.

Testatoo uses deferred evaluation:: As you can see, we instantiate a ListView event if the component is not available in the page.
                                   You can test if the component is missing

Testatoo DSL is very clean and functional:: Testatoo is based on a clear and powerful syntax.
Testatoo code never refers to a technical layer:: Under no circumstances in this test does the underline DOM technology transpire in our code.

[NOTE]
===============================
As a rule, when you create a test with Testatoo, we recommend to add some default imports to facilitate the automatic completion in your favorite IDE(A).

[source, groovy]
-------------------------------------------------------------------------------

import static org.testatoo.core.Testatoo.*
import static org.testatoo.core.input.Mouse.*
import static org.testatoo.core.property.Properties.*
import static org.testatoo.core.state.States.*

-------------------------------------------------------------------------------
===============================

== Back to the future

Well well well (My Michelle), as explained, Testatoo is a library to write functional tests. Functional tests mean SPECIFICATIONS.

As a SPECIFIER, I want to express my test (intention) BEFORE I code my UI. So, if the intention is to have
a page with two radio buttons to select the gender (male or female), the test can be expressed like this :

-------------------------------------------------------------------------------
assert that the maleRadio is unchecked
and it has label "Male"

assert that the femaleRadio is unchecked
and it has label "Female"

check the maleRadio
assert that the maleRadio is checked
assert that the femaleRadio is unchecked

check the femaleRadio
assert that the femaleRadio checked
assert that the maleRadio is unchecked
-------------------------------------------------------------------------------

The Testatoo syntax to express this specification is :

[source, groovy]
-------------------------------------------------------------------------------
expect {
    maleRadio.is(unchecked) and maleRadio.has label('Male')
    femaleRadio.is(unchecked) and femaleRadio.has label('Female')
}

check maleRadio
expect maleRadio is checked
expect femaleRadio is unchecked

check femaleRadio
expect femaleRadio is checked
expect maleRadio is unchecked
-------------------------------------------------------------------------------

Pretty close, isn't it? ;)

Testatoo provides a DSL to write tests in a natural language, easy to manipulate from a specifications point of view.
As this is a high level language, it allows tests to be written before UI coding.

YES, for the first time, with Testatoo, you can write UI functional tests FIRST (yeah I know, it's pretty cool!)

== How Testatoo works

1 - The browser is launched through Webdriver

[source, groovy]
-------------------------------------------------------------------------------
Testatoo.evaluator =  new WebDriverEvaluator(new FirefoxDriver())
-------------------------------------------------------------------------------

You can add all the required "DesiredCapabilities" on your WebDriver config

2 - The application under test's URL is opened

[source, groovy]
-------------------------------------------------------------------------------
open 'http://www.google.com'
-------------------------------------------------------------------------------

3 - Testatoo injects the isolated (noConflict mode) jQuery lib in the application under test. Testatoo uses the jQuery functional API
to make complex DOM evaluations.

== The Testatoo API: How to interact with content

=== The $ Function

The $ function is the access point of component creation. The $ function is the strict equivalent of the popular jQuery $ function.
In fact, in Testatoo the $ groovy function is a wrapper on the underlying jQuery function.

The signature of the $ function is as followsâ€¦

[source, groovy]
-------------------------------------------------------------------------------
$('css selector');

// Samples
$('#myId');
$('.myClass');
$('ul li:first-child');
$('[data-role=button]');
-------------------------------------------------------------------------------

Our experience demonstrated that we never need to use complex selector. If you write tests first, the component tested carries
a semantic and this semantic must be reflected in the code by an ID or a role (data-role attribute is commonly used in web development).

Once the component is selected, it must be *TYPED* throw the *"as"* keyword.

[source, groovy]
-------------------------------------------------------------------------------
TextField textField = $('#text_field') as TextField
Button button = $('#button') as Button
-------------------------------------------------------------------------------

But wait a moment: How can you know if a button is a button!? Indeed, in  HTML, a Button can be represented in many ways.

[source, html]
-------------------------------------------------------------------------------
<button id="button">My Button</button>
<input id="button" type="button" value="My Button">
<input id="button" type="submit" value="My Button">

-------------------------------------------------------------------------------

This is the magic of Testatoo. We will see later how this works and how to override this behavior, to easily add new component types.

=== The components

Testatoo comes with a bunch of predefined HTML components. These components are not only the standard representation of HTML elements,
but also a powerful base to build your own components. Each component supports States and Properties. One of the strength of Testatoo
is the capability to add support for new States and Properties and to override existing States and Properties, on existing or new components.

[options="header"]
|=========================================================================================================================================================================
| Component               |                   Properties                                                      |                    States
|      *Button*           |   Text                                                                            |   *
|      *Form*             |                                                                                   |   *
|      *Heading*          |   Text                                                                            |   *
|      *Image*            |   Source                                                                          |   *
|      *Link*             |   Text,  Reference                                                                |   *
|      *Panel*            |   Title                                                                           |   *
|      *DropDown*         |   Label, GroupItemsSize, SelectedItems, Size, Items GroupItems                    |   *
|      *GroupItem*        |   Label, Value, Size, Items                                                       |   *
|      *Item*             |   Label, Value                                                                    |   *, Selected, UnSelected
|      *ListBox*          |   Label, GroupItemsSize, VisibleItemsSize, SelectedItems, Size, Items, GroupItems |   *, MultiSelectable, SingleSelectable
|      *ListView*         |   Size. Items                                                                     |   *
|      *DataGrid*         |   Size, ColumnSize, RowSize                                                       |   *
|      *Column*           |   Size, Title                                                                     |   *
|      *Cell*             |   Value                                                                           |   *
|      *Row*              |   Size                                                                            |   *
|      *CheckBox*         |   Label                                                                           |   *, Checked, Unchecked
|      *ColorField*       |   Placeholder, Label, Text, Value                                                 |   *, Empty, Filled
|      *DateField*        |   Minimum, Maximum, Step, Placeholder, Label, Text, Value                         |   *, Empty, Filled
|      *DateTimeField*    |   Placeholder, Label, Text, Value                                                 |   *, Empty, Filled
|      *EmailField*       |   Placeholder, Label, Text, Value                                                 |   *, Empty, Filled
|      *MonthField*       |   Placeholder, Label, Text, Value                                                 |   *, Empty, Filled
|      *NumberField*      |   Minimum, Maximum, Step, Placeholder, Label, Text, Value                         |   *, Empty, Filled
|      *PasswordField*    |   Placeholder, Label, Text, Value                                                 |   *, Empty, Filled
|      *PhoneField*       |   Pattern, Placeholder, Label, Text, Value                                        |   *, Empty, Filled
|      *Radio*            |   Label                                                                           |   *, Checked, Unchecked
|      *RangeField*       |   Minimum, Maximum, Step, Placeholder, Label, Text, Value                         |   *, Empty, Filled
|      *SearchField*      |   Placeholder, Label, Text, Value                                                 |   *, Empty, Filled
|      *TextField*        |   Placeholder, Label, Text, Value                                                 |   *, Empty, Filled
|      *TimeField*        |   Placeholder, Label, Text, Value                                                 |   *, Empty, Filled
|      *URLField*         |   Placeholder, Label, Text, Value                                                 |   *, Empty, Filled
|      *WeekField*        |   Placeholder, Label, Text, Value                                                 |   *, Empty, Filled
|=========================================================================================================================================================================


[*]: Enabled, Disabled, Available, Missing, Hidden, Visible

[NOTE]
===============================
All Testatoo components inherit from the base class Component.groovy
===============================

Evaluate a property





Evaluate a State


=== The Action

==== Mouse

==== Keyboard


=== Under the hood








